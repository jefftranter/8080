                        *** **************************************************
                        *
                        *       DEMO: MTR-88
                        *
                        *       SYSTEM DEFINITIONS
                        *
      = 002.136         ALARM   EQU     2136A          ;MAKE NOISE
      = 003.302         WCC     EQU     3302A          ;WRITE CHAR TO CONSOLE
      = 040.010         .MFLAG   EQU    40010A         ;USER FLAG OPTIONS
      = 040.037         UIVEC   EQU     40037A         ;USER INTERRUPT VECTOR
      = 000.001         UO.CLK  EQU     1A             ;ALLOW CLOCK INTERRUPT PROCESSING
      = 000.303         MI.JMP  EQU     303A           ;MACHINE INSTRUCTION JUMP
      = 003.066         IOB     EQU     3066A          ;INPUT OCTAL BYTE
      = 005.343         TOB     EQU     5343A          ;TYPE OCTAL BYTE

      = 000.033         ESC     EQU     33A
      = 000.015         CR      EQU     15A
      = 000.012         LF      EQU     12A

040.100                         ORG     40100A

                        *** **************************************************
                        *
                        *       ERASE SCREEN
                        *
040.100  076 033        MTR88   MVI     A,ESC          ;ESCAPE SEQUENCE TO
040.102  315 302 003            CALL    WCC
040.105  076 105                MVI     A,'E'              ;ERASE SCREEN
040.107  315 302 003            CALL    WCC

                        *** **************************************************
                        *
                        *       READ A OCTAL INTEGER FROM KEYBOARD
                        *          STORE THE NUMBER.
                        *
040.112  041 234 040            LXI     H,NUMBER       ;GET ADDRESS OF NUMBER
040.115  247                    ANA     A              ;CLEAR CARRY (SIDE EFFECT)
040.116  315 066 003            CALL    IOB            ;INPUT OCTAL BYTE
040.121  315 223 040            CALL    SETICK         ;SETUP TICK TO 500 FOR ONE SEC

                        *** **************************************************
                        *
                        *       INITIALIZE SERVICE INTERRUPT ROUTINE
                        *          LOAD THE USER INTERRUPT VECTOR (UIVEC) WITH A
                        *          JUMP INSTRUCTION AND THE ADDRESS OF THE SERVICE
                        *          ROUTINE.  ENABLE USER CLOCK INTERRUPT!
                        *
040.124  076 303                MVI     A,MI.JMP       ;SET-UP JUMP INSTRUCTION
040.126  062 037 040            STA     UIVEC          ;STORE 'JMP' INSTRUCTION
040.131  041 165 040            LXI     H,INTRP           ;USER INTERRUPT ADDRESS
040.134  042 040 040            SHLD    UIVEC+1              ;POSITIONED
040.137  076 001                MVI     A,UO.CLK
040.141  062 010 040            STA     .MFLAG         ;ENABLE CLOCK INTERRUPT

                        *** **************************************************
                        *
                        *       WAIT FOR CLOCK TO REACH ZERO
                        *
040.144  072 234 040    LOOP    LDA     NUMBER         ;DO NOTHING LOOP.
040.147  376 000                CPI     0                 ;WAIT FOR END
040.151  302 144 040            JNZ     LOOP              ;OF COUNT DOWN.

                        *** **************************************************
                        *
                        *       RETURN TO NORMAL INTERRUPT STATUS & HALT
                        *          DISABLE INTERRUPT & TURN ON SPEAKER
                        *
040.154  076 000                MVI     A,0
040.156  062 010 040            STA     .MFLAG         ;DISABLE CLOCK INTERRUPT
040.161  315 136 002            CALL    ALARM
040.164  166                    HLT

                        *** **************************************************
                        *
                        *       INTERRUPT ROUTINE
                        *          CLOCK AND DISPLAY INTERRUPT
                        *
040.165  052 232 040    INTRP   LHLD    TICK           ;GET COUNT (BETWEEN 0 & 500)
040.170  053                    DCX     H              ;TICK=TICK-1
040.171  042 232 040            SHLD    TICK           ;STORE COUNT
040.174  175                    MOV     A,L            ;TEST FOR ZERO
040.175  264                    ORA     H                 ;COMPARE WITH 'H'
040.176  300                    RNE                        EXIT;IF .NE. 0

                        *** **************************************************
                        *
                        *       UPDATE DISPLAY FOR 'NEW' NUMBER.
                        *
040.177  076 015                MVI     A,CR           ;DO CARRIAGE RETURN
040.201  315 302 003            CALL    WCC
040.204  076 012                MVI     A,LF              ;AND LINE FEED
040.206  315 302 003            CALL    WCC
040.211  072 234 040            LDA     NUMBER         ;GET NUMBER
040.214  075                    DCR     A              ;NUMBER=NUMBER-1
040.215  062 234 040            STA     NUMBER         ;SAVE NUMBER
040.220  315 343 005            CALL    TOB            ;TYPE OCTAL BYTE
040.223  041 364 001    SETICK  LXI     H,500          ;RESTORE COUNT
040.226  042 232 040            SHLD    TICK             ;WITH 500
040.231  311                    RET

                        *       STORAGE AREA & END ASSEMBLY
040.232   (000.002)     TICK    DS      2
040.234   (000.001)     NUMBER  DS      1
040.235   (040.100)             END     MTR88

00000 Total Errors
00000 Total Warnings

.DATE.             0001 E  .MFLAG             2008 E  .TIME.             0002 E
ALARM              025E E  CR                 000D E  ESC                001B E
INTRP              2075    IOB                0336 E  LF                 000A E
LOOP               2064    MI.JMP             00C3 E  MTR88              2040
NUMBER             209C    SETICK             2093    TICK               209A
TOB                05E3 E  UIVEC              201F E  UO.CLK             0001 E
WCC                03C2 E
